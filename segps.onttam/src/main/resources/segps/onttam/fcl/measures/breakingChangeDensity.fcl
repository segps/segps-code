/*
 * FCL file for the Breaking Change Density measure
 */
FUNCTION_BLOCK BCD

VAR_INPUT
	BCD_Measure: REAL;
	BCD_Weight: REAL;
END_VAR

VAR_OUTPUT
	BCD_Score: REAL;
END_VAR

FUZZIFY BCD_Measure
	TERM VERYLOW := (0.0,1.0) (0.13125,0.0)  ;
	TERM LOW := (0.11812500000000001,0.0) (0.25,1.0) (0.39375,0.0) ;
	TERM AVERAGE := (0.354375,0.0) (0.5,1.0) (0.65625,0.0) ;
	TERM HIGH := (0.590625,0.0) (0.75,1.0) (0.91875,0.0) ;
	TERM VERYHIGH := (0.8268749999999999,0.0) (1.0,1.0) (1.3232799187227053,1.0) ;
END_FUZZIFY

FUZZIFY BCD_Weight
	TERM LOW := (0.0,1.0) (0.5,1.0) (2.69,0.0) ;
	TERM MEDIUM := (2.56,0.0) (4.75,1.0) (7.05,0.0) ;
	TERM HIGH := (6.69,0.0) (9.0,1.0) (12.0,1.0) ;
END_FUZZIFY

DEFUZZIFY BCD_Score
	TERM VERYPOOR := (6.5,0.0) (7.5,1.0) (9.0,1.0) ;
	TERM POOR := (5.31,0.0) (6.25,1.0) (7.22,0.0) ;
	TERM AVERAGE := (4.14,0.0) (5.0,1.0) (5.9,0.0) ;
	TERM VERYGOOD := (2.95,0.0) (3.75,1.0) (4.6,0.0) ;
	TERM EXCELLENT := (0.0,1.0) (2.5,1.0) (3.28,0.0) ;
	METHOD : COG;
END_DEFUZZIFY

RULEBLOCK BCD_SCORE_RULES
	RULE 0 : IF BCD_Measure IS VERYLOW AND BCD_Weight IS LOW THEN BCD_Score IS EXCELLENT ;
	RULE 1 : IF BCD_Measure IS VERYLOW AND BCD_Weight IS MEDIUM THEN BCD_Score IS EXCELLENT ;
	RULE 2 : IF BCD_Measure IS VERYLOW AND BCD_Weight IS HIGH THEN BCD_Score IS VERYGOOD ;
	
	RULE 3 : IF BCD_Measure IS LOW AND BCD_Weight IS LOW THEN BCD_Score IS EXCELLENT ;
	RULE 4 : IF BCD_Measure IS LOW AND BCD_Weight IS MEDIUM THEN BCD_Score IS VERYGOOD ;
	RULE 5 : IF BCD_Measure IS LOW AND BCD_Weight IS HIGH THEN BCD_Score IS AVERAGE ;
	
	RULE 6 : IF BCD_Measure IS AVERAGE AND BCD_Weight IS LOW THEN BCD_Score IS VERYGOOD ;
	RULE 7 : IF BCD_Measure IS AVERAGE AND BCD_Weight IS MEDIUM THEN BCD_Score IS AVERAGE ;
	RULE 8 : IF BCD_Measure IS AVERAGE AND BCD_Weight IS HIGH THEN BCD_Score IS POOR;
	
	RULE 9 : IF BCD_Measure IS HIGH AND BCD_Weight IS LOW THEN BCD_Score IS AVERAGE ;
	RULE 10 : IF BCD_Measure IS HIGH AND BCD_Weight IS MEDIUM THEN BCD_Score IS POOR ;
	RULE 11 : IF BCD_Measure IS HIGH AND BCD_Weight IS HIGH THEN BCD_Score IS  VERYPOOR;
	
	RULE 12 : IF BCD_Measure IS VERYHIGH AND BCD_Weight IS LOW THEN BCD_Score IS POOR ;
	RULE 13 : IF BCD_Measure IS VERYHIGH AND BCD_Weight IS MEDIUM THEN BCD_Score IS VERYPOOR ;
	RULE 14 : IF BCD_Measure IS VERYHIGH AND BCD_Weight IS HIGH THEN BCD_Score IS VERYPOOR ;
END_RULEBLOCK

END_FUNCTION_BLOCK
